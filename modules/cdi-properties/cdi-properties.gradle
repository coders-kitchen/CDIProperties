apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'jacoco'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

jacocoTestReport{
	reports {
		xml.enabled true
		csv.enabled false
		html.destination "${buildDir}/jacocoHtml"
	}
	onlyIf{true}
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
	classifier 'javadoc'
	from "${docsDir}/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
	archives javaDocJar
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile libraries.cdi_1_0
	testCompile libraries.testing.junitAndMockito
}

if (hasProperty('sonatypeUsername')) {
	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
				pom {
					project {
						name 'CDIProperties'
						packaging 'jar'
						description 'CDI Extgension that adds property loading via annotations to CDI'
						url 'http://coders-kitchen.github.com'

						scm {
							url 'scm:git@github:CodersKitchen/CDIProperties.git'
							connection 'scm:git@github:CodersKitchen/CDIProperties.git'
							developerConnection 'scm:git@github:CodersKitchen/CDIProperties.git'
						}

						licenses {
							license {
								name 'The MIT License (MIT)'
								url 'http://opensource.org/licenses/MIT'
								distribution 'repo'
							}
						}


						developers {
							developer {
								id 'peterdaum'
								name 'Peter Daum'
							}
						}
					}
					artifactId = project.name.toLowerCase()
				}
			}
		}
	}
}
